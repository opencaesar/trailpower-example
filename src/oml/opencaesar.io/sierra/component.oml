vocabulary <http://opencaesar.io/sierra/component#> as component {

	extends <http://www.w3.org/2002/07/owl#> as owl

	extends <http://opencaesar.io/sierra/base#> as base

	extends <http://opencaesar.io/sierra/operation#> as operation

	extends <http://opencaesar.io/sierra/requirement#> as requirement

	extends <http://opencaesar.io/sierra/function#> as function

	// Aspects

	aspect PresentingElement

	// Concepts

	concept Component < base:ContainableElement, base:SpecializableElement, requirement:SatisfyingElement, function:PerformingElement, PresentingElement [
		restricts all base:contains to Component
		restricts all base:isContainedBy to Component
		restricts all base:specializes to Component
		restricts all base:isSpecializedBy to Component
	]

	concept System < Component [
		restricts all base:isContainedBy to System
		restricts all base:specializes to System
		restricts all base:isSpecializedBy to System
	]

	concept Interface

	concept Path < base:AggregatableElement [
		restricts all base:aggregates to Connector
		restricts all base:isAggregatedBy to owl:Nothing
	]

	// Relations

	relation entity Connector [
		from Interface
		to Interface
		forward sendsTo
		reverse receivesFrom
	] < base:AggregatableElement [
		restricts all base:aggregates to owl:Nothing
		restricts all base:isAggregatedBy to Path
	]

	relation presents [
		from PresentingElement
		to Interface
		reverse isPresentedBy
		inverse functional
	]

	relation supports [
		from System
		to operation:Capability
		reverse isSupportedBy
		inverse functional
	]

}