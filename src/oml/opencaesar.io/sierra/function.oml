vocabulary <http://opencaesar.io/sierra/function#> as function {

	extends <http://www.w3.org/2002/07/owl#> as owl

	extends <http://opencaesar.io/sierra/base#> as base

	extends <http://opencaesar.io/sierra/component#> as component

	extends <http://opencaesar.io/sierra/capability#> as capability

	aspect ExchangingElement

	relation entity Exchange [
		from ExchangingElement
		to ExchangingElement
		forward sendsTo
		reverse receivesFrom
	] < base:AggregatableElement [
		restricts all base:aggregates to owl:Nothing
		restricts all base:isAggregatedBy to Chain
	]

	concept Item

	relation conveys [
		from Exchange
		to Item
		reverse isConveyedBy
		functional
	]

	concept Chain < base:AggregatableElement [
		restricts all base:aggregates to Exchange
		restricts all base:isAggregatedBy to owl:Nothing
	]

	relation implements [
		from Chain
		to capability:Capability
		reverse isImplementedBy
		functional
		inverse functional
	]

	aspect PerformingElement

	ref concept capability:Actor < PerformingElement

	ref concept capability:Entity < PerformingElement

	ref concept component:Component < PerformingElement

	concept Function < ExchangingElement

	relation performs [
		from PerformingElement
		to Function
		reverse isPerformedBy
	]

	concept Pin < ExchangingElement

	relation hasPin [
		from Function
		to Pin
		reverse isPinOf
		inverse functional
	]

	concept Input < Pin

	relation hasInput [
		from Function
		to Input
		reverse isInputOf
	] < hasPin

	concept Output < Pin

	relation hasOutput [
		from Function
		to Output
		reverse isOutputOf
	] < hasPin

}