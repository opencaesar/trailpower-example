vocabulary <http://opencaesar.io/sierra/function#> as function {

	extends <http://www.w3.org/2002/07/owl#> as owl

	extends <http://opencaesar.io/sierra/base#> as base

	extends <http://opencaesar.io/sierra/datatype#> as datatype

	extends <http://opencaesar.io/sierra/operation#> as operation

	extends <http://opencaesar.io/sierra/requirement#> as requirement

	// Aspects

	aspect PerformingElement

	aspect ExchangingElement

	// Concepts

	concept Function < requirement:SatisfyingElement, ExchangingElement

	concept Pin < ExchangingElement

	concept InputPin < Pin

	concept OutputPin < Pin

	// Relations

	relation entity Exchange [
		from ExchangingElement
		to ExchangingElement
		forward sendsTo
		reverse receivesFrom
	]

	relation transmits [
		from Exchange
		to datatype:DataType
		reverse isTransmittedBy
		functional
	]

	relation implements [
		from Exchange
		to operation:Capability
		reverse isImplementedBy
	]

	relation performs [
		from PerformingElement
		to Function
		reverse isPerformedBy
		inverse functional
	]

	relation hasPin [
		from Function
		to Pin
		reverse isPinOf
		inverse functional
	]

	relation hasInput [
		from Function
		to InputPin
		reverse isInputOf
	] < hasPin

	relation hasOutput [
		from Function
		to OutputPin
		reverse isOutputOf
	] < hasPin

}	